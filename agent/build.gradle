plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "org.jetbrains.kotlin.jvm" version "1.3.72"
    id "org.jlleitschuh.gradle.ktlint" version "9.2.1"
    id "io.gitlab.arturbosch.detekt" version "1.9.0"
}

description = "The Agent."

repositories {
    jcenter {
        content {
            includeGroup "org.jetbrains.kotlinx"
        }
    }
}

dependencies {
    // Kotlin
    implementation group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk8", version: property("kotlin.version")
    implementation group: "org.jetbrains.kotlin", name: "kotlin-reflect", version: property("kotlin.version")

    // Bytecode instrumentation
    implementation group: "org.ow2.asm", name: "asm", version: property("asm.version")
    implementation group: "org.ow2.asm", name: "asm-util", version: property("asm.version")
    implementation group: "net.bytebuddy", name: "byte-buddy", version: property("byte-buddy.version")

    // Other projects
    implementation(project(":agent-lib"))
    implementation(project(":util"))
    implementation(project(":logging"))

    // JUnit
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-api", version: property("junit.version")
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-engine", version: property("junit.version")

    // Need the agent (yes, THIS project) shadow jar at runtime to dynamically load the agent-lib
    testRuntime project(path: ":agent", configuration: "shadow")
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "14"
    }
}

spotless {
    kotlin {
        ktlint(property("ktlint.version") as String)
        licenseHeaderFile(rootProject.rootDir.toPath().resolve("config").resolve("spotless").resolve("omj.license"))
    }
}

detekt {
    parallel = true
    config = files(rootProject.rootDir.toPath().resolve("config").resolve("detekt").resolve("config.yml"))
}

jar {
    manifest {
        attributes(
                "Premain-Class": "com.octogonapus.omj.agent.Agent",
                "Can-Redefine-Classes": "true",
                "Can-Retransform-Classes": "true",
                "Can-Set-Native-Method-Prefix": "true"
        )
    }
}

def agentLibJarName = "agent-lib-all"
def agentLibAllJarResource = buildDir.toPath().resolve(agentLibJarName)

def copyAgentLibShadowJarTask = task("copyAgentLibShadowJar", type: Copy) {
    dependsOn(":agent-lib:shadowJar")
    from({ project(":agent-lib").tasks.named("shadowJar") })
    into(buildDir)
    rename { agentLibJarName }
}

shadowJar {
    dependsOn(copyAgentLibShadowJarTask)
    from({ agentLibAllJarResource })
}
